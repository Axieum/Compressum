plugins {
    id 'java'
    id 'maven'
    id 'signing'
}

group "${GROUP}.${ID}"
version "${VERSION_MAJOR}.${VERSION_MINOR}.${VERSION_REVIS}"

sourceCompatibility = targetCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'commons-io', name: 'commons-io', version: '2.6'
    compile group: 'org.apache.commons', name: 'commons-compress', version: '1.18'
    compile group: 'org.tukaani', name: 'xz', version: '1.8'

    testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.3.1'
    testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: '5.3.1'
    testRuntime group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.3.1'

    compile fileTree(dir: "lib", include: "*.jar")
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from tasks.javadoc.destinationDir
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar

    archives javadocJar
    archives sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: sonatypeId, password: sonatypePassphrase)
            }

            pom.project {
                name 'Compressum'
                packaging 'jar'
                description 'A Java library for easily handling archive creation'
                url 'https://github.com/Axieum/Compressum'

                scm {
                    url 'https://github.com:Axieum/Compressum'
                    connection 'scm:git:git@github.com:Axieum/Compressum.git'
                    developerConnection 'scm:git:ssh://git@github.com:Axieum/Compressum.git'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'axieum'
                        name 'Axieum'
                    }
                }
            }
        }
    }
}
